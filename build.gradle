apply plugin: 'java'
apply plugin: 'maven'

group = 'com.welty'
description = 'nboard'
version = '0.01-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7
compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDirs java.srcDirs as File[]
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDirs java.srcDirs as File[]
        }
    }
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } {
        // remove all signature files
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes 'Implementation-Title': description,
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Main-Class': 'com.welty.othello.gui.Viewer'
    }
}

repositories {
    mavenLocal()
    maven { url "http://repository.jboss.com/maven2" }
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile 'org.jetbrains:annotations:8.1'
    compile 'net.sf.trove4j:trove4j:3.0.3'
    compile 'com.orbanova:common:0.07-SNAPSHOT'
    compile 'com.welty:graph:0.01-SNAPSHOT'
    compile 'com.welty:othello:1.0-SNAPSHOT'
    compile 'com.welty:novello:0.01-SNAPSHOT'
    testCompile 'org.easymock:easymock:3.0'
    testCompile 'org.mockito:mockito-all:1.8.5'
    testCompile 'junit:junit:4.4'
    provided 'org.projectlombok:lombok:1.12.2'
}

buildscript {
    repositories {
        flatDir dirs: 'C:\\dist\\proguard4.10\\lib'
    }
    dependencies {
        classpath ':proguard'
    }
}

task proguard(type: proguard.gradle.ProGuardTask) {
    configuration 'myconfig.pro'
    forceprocessing
}